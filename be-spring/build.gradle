plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.8'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id "org.openapi.generator" version "6.2.1"
}

group = 'hu.aestallon'
version = '0.0.1-SNAPSHOT'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'io.swagger.core.v3:swagger-annotations:2.2.6'
    implementation 'org.springframework.boot:spring-boot-starter-validation:2.7.1'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.4'
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('processFrontendResources', Copy) {
    def frontendBuildDir = file("${project(':adrestore-ui-angular').buildDir}/../dist/ui-angular")
    def frontendRsrcsDir = file("${project.buildDir}/resources/main/static")

    group 'Frontend'
    description 'Process Frontend Resources'
    dependsOn project(':adrestore-ui-angular').tasks.named('assembleFrontend')

    println "from: $frontendBuildDir to: $frontendRsrcsDir"

    from frontendBuildDir
    into frontendRsrcsDir
}

tasks.named('processResources') {
    dependsOn tasks.named('processFrontendResources')
}

openApiGenerate {
    generatorName   = "spring"
    inputSpec       = "$rootDir/adrestore.yaml".toString()
    outputDir       = "$rootDir/be-spring".toString()
    apiPackage      = "hu.aestallon.adrestore.rest.api"
    modelPackage    = "hu.aestallon.adrestore.rest.model"
    configOptions   = [
            dateLibrary             : "java8",
            delegatePattern         : "true",
            fullJavaUtil            : "true",
            library                 : "spring-boot",
            documentationProvied    : "none",
            useOptional             : "true",
            useSwaggerUI            : "false",
            hideGenerationTimestamp : "true"
    ]
    globalProperties = [
            apis            : "",
            models          : ""
    ]
    supportingFilesConstrainedTo = [
            "ApiUtil.java",
            "RFC3339DateFormat.java"
    ]
}